use npc;
use os;
use uo;

include "include/eventid";
include "include/npcbackpacks";
include "include/npccast";
include "ai/setup/modsetup";
include "include/random";
include "include/attributes";

const HALT_THRESHOLD := 15;
Const MOVING_EFFECT_FIREBALL  := 0x36D4; 
Const STARTHAIR := 0x2030;
Const ENDHAIR := 0x2060;

var me := Self();
var masterserial := GetObjProperty( me , "master" );
var master := SystemFindObjectBySerial( masterserial );
var mypack := FindMyPack(me.serial);
var now;
var lastvamp;
var mymana;
var specbonus;
var todrain;

var saywords := 1;
var summons := 0;
var chaseloop := 0;

var following := 0;
var nextwatch := ReadGameClock();

me.setmaster( master );

if (!me.backpack)
	var newbackpack := CreateItemAtLocation(5288 , 1176 , 0, 0xe75,1);
	EquipItem(me, newbackpack);
endif

program TamedAI()

	if (getobjproperty(me, "frozen"))
		me.frozen := 1;
	endif

	SetAnchor( me.x, me.y, 20, 0 );

    EnableEvents( SYSEVENT_SPEECH, 12 );
    EnableEvents( SYSEVENT_ENGAGED + SYSEVENT_DAMAGED );

    SetWarMode( 0 );
    SetObjProperty(me, "serial", me.serial);

    MainAILoop();

endprogram

function CloseDistance( opponent )

	var sleepdelay := 300 - (CInt(GetDexterity(me)) * 1.5);
	if (sleepdelay < 0) 
		sleepdelay := 0; 
	endif
	var MoveSpeed := GetObjProperty(me, "MoveSpeed");
	if(MoveSpeed)
		sleepdelay := GetSpeed(me, MoveSpeed);
	endif
  
    case (Distance( me, opponent ))
        1:
        0:
            return 1;
        default:
            if(!RunToward( opponent ))
            Sleepms(sleepdelay);
               return 1;
            else
               return 0;
            endif
    endcase

endfunction

function Fetch()

	var tobj := Target(master);   //object I'm supposed to get
	if (tobj.container)        
		return;
	endif

	SayOk();

	var times := 0;
	while ( (distance(me, tobj) > 1) and times < 10 and CheckLineOfSight(me,tobj) )
		if( !runtoward(tobj))
			sleepms(500);
			times := times + 1;
		else
			times := 0;
		endif
	endwhile

	if ( (tobj.movable) and (accessible(me,tobj)) )
		MoveItemToContainer(tobj, mypack);
	endif

endfunction

function drop()

	SayWhat();
	foreach myitems in enumerateitemsincontainer(mypack)
		if (myitems.container.serial == mypack.serial)
        		moveObjecttolocation(myitems, me.x, me.y, me.z);
			sleepms(100);
		endif
	endforeach

endfunction

function MainAILoop()

    var ev;
    var evtext;
    var mename := lower(me.name);

    nextwatch := ReadGameClock();
    var waittime := 0;

    while (1)
        ev := os::wait_for_event( waittime );
        case (ev.type)
	SYSEVENT_SPEECH:
		ProcessSpeech(ev);
	SYSEVENT_ENGAGED:
	SYSEVENT_DAMAGED:
		if (ev.source)
			Fight( ev.source );
		endif
	EVID_ALL_FOLLOW_CMD:
		following := master;
        endcase

	if (following) 
		waittime := 0;
		Follow();
	elseif (master)
		waittime := 7;
		Guard();
	else
		waittime := 120;
	endif

    endwhile

endfunction

function Guard()

	if (ReadGameClock() > NextWatch)
		if (!master.dead)
			if (CheckLineOfSight(me,master))
				if (( GetHP(master) < (GetMaxHP(master)/2)) && (!GetObjProperty( master, "IsLiche" )))
					CastSpell( me , master, "gheal");
					sleep(3);
				endif
				if (master.poisoned)
					CastSpell( me, master, "cure");
					sleep(3);
				endif
			else
				if (Random(20)==1 and following)
					TeletoMaster();
				endif
			endif
		endif
		NextWatch := ReadGameClock()+5;
		if (Random(10)==1)
			SayWhat();
		endif
	endif

endfunction

function Follow()

	if ( (me.frozen) )
		sleep(10);
		return;
	endif

	SetDexterityMod(me, 5000);
	var d := Distance( me, following );
	if (d < 2)
		sleep(1);
		chaseloop := 0;
	elseif (( d > 10 ) or ( chaseloop > 50 ))
		MoveObjectToLocation( me, following.x, following.y, following.z, master.realm,
			MOVEOBJECT_FORCELOCATION );
		chaseloop := 0;
		sleep(1);
	else
		RunToward(following);
		chaseloop := chaseloop + 1;
	endif

	if ( (!GetBaseStrength(following)) or (following.dead) )
		following := 0;
	endif
	SetDexterityMod(me, 0);
	if (master)
		Guard();
	endif

endfunction

function ProcessSpeech(ev)

	if( ev.source.serial != masterserial )
		return;
	endif

	if (!master)
		RestartScript( me );
	endif

	if (me.name != master.name)
		me.name := master.name;
	endif

	sayOk();

	var evtext := lower(ev.text);
	var mename := lower(me.name);

	if( evtext[mename + " stop"] or evtext["all stop"])
		following := 0;
	elseif ((evtext[mename + " come"]) or (evtext["all come"]))
		var done := 0;
		chaseloop := 0;
		while( done == 0 )
			chaseloop := chaseloop +1;
			done := CloseDistance( ev.source );
			if (chaseloop > 25)
				done :=1;
			endif
		endwhile
	elseif (evtext["follow"])
		following  := ev.source;
	elseif (evtext["fetch"]  or (evtext["get"]))
		fetch();
	elseif (evtext["drop"])
		drop();
	elseif (evtext[mename + " speak"]  or (evtext["all speak"]))
		SayOk();
	elseif (evtext["cureself"])
		castspell(me,me,"cure");
		sleep(3);
	elseif (evtext["cure"])
		castspell(me,master,"cure");
		sleep(3);
	elseif (evtext["healself"])
		castspell(me,me,"gheal");
		sleep(3);
	elseif (evtext["heal"])
		castspell(me,master,"gheal");
		sleep(3);
	elseif (evtext["light"])
		castspell(me,master,"nightsight");
		sleep(3);
	elseif (evtext["protect"])
		castspell(me,master,"protection");
		sleep(3);
	elseif (evtext["bless"])
		castspell(me,master,"bless");
		sleep(3);
	elseif (evtext["loot"])
		GrabLoot();
		return 1;
	elseif (evtext["give"])
		GiveLoot();
		return 1;
	elseif (evtext["vamp"])
		lastvamp := GetObjProperty(me, "lastvamp");
		now := ReadGameClock() ;
		if ((now - lastvamp) > 600)
			Setobjproperty(me,"lastvamp",now);
			specbonus := GetObjProperty(master,"IsMage");
			todrain := (50 +(specbonus * Random(20)));
			if (todrain > master.mana)
				todrain := master.mana;
			endif
			SetMana(master, GetMana(master)-todrain);
			SetMana(me, GetMana(me)+todrain);
			PrintTextAbove( me, "Ort Sanct" );
			PerformAction( me, 0X010 );
			PlaySoundEffect(master,0x01fa);
			PlayObjectCenteredEffect(master, 0x37b9,10, 30);
		else
			PrintTextAbove( me, "Ort Sanct" );
			PerformAction( me, 0X010 );
			PlaySoundEffect(me,0x05d);
			PlayObjectCenteredEffect(me, 0x3735,10, 30);
		endif	
		sleep (3);
	elseif (evtext["hide"])
		me.hidden := 1;
		sleep(3);	
	elseif( evtext[mename + " release"] or evtext["all release"])
		Release();
		return 1;
	endif

endfunction

function AllCommand(evtype,what)

	var mobs := ListMobilesNearLocation( master.x, master.y, master.z, 9 );
	var eve array;
	eve.+type := evtype;
	eve.+source := me;
	eve.+target := what;
	SendEvent( me , eve );
	foreach mob in mobs 
		if( GetObjProperty( mob , "master" ) == me.master.serial )
			SendEvent( mob , eve );
		endif
	endforeach

endfunction

function cast_meteor()

    var targets := ListMobilesInLineofSight(me, 3);

	targets := SmartAoE(master, targets);

    foreach mobile in targets
	   var dmg := Random((cint( cint(GetMaxHP(me)/2.5) * GetHP(me))/GetMaxHP(me)))+5;
	   PlayMovingEffect(me, mobile ,MOVING_EFFECT_FIREBALL, 10,1,1);
	   applydamage(mobile, dmg);           
    endforeach

endfunction

function fight(opponent)

	SetDexterityMod(me, 0);
	if (opponent.serial == me.serial)
		return;
	endif
	SayWhat();

	following := 0;
	var runs := 0;
	while ( (distance(me, opponent) < 20) and (runs < 50) )
		runawayfrom(opponent);
		runs := runs +1;
		wait_for_event(0);
	endwhile

	me.hidden := 1;

endfunction

function SayWhat()

	if (me.graphic == 0x3A)
		PlaySoundEffect( me , 0x1D3 );//wisp
	else
		PlaySoundEffect( me , 0x259 );//humuc
	endif

endfunction

function SayOK()

	if (me.graphic == 0x3A)
		PlaySoundEffect( me , 0x1D4 );//wisp
	else
		PlaySoundEffect( me , 0x254 );//humuc
	endif

endfunction

function giveloot();

	SayWhat();
	var who := master;
	var bag := Target(who);
	if (!bag)
		return;
	endif

	if (bag.npctemplate)
		return;
	elseif (bag.acctname)
		who := bag;
	endif

	var done := 0;
	chaseloop := 0;
	while( done == 0 and chaseloop < 50 )
		chaseloop := chaseloop +1;
		done := CloseDistance( who );
	endwhile

	if (!done)
		return;
	endif

	if (bag.container.serial != who.backpack.serial)
		bag := who.backpack;
	endif

	var stackeditems := {};

	foreach item in enumerateitemsincontainer(bag)
		if (item.amount > 1)
			stackeditems[len(stackeditems)+1]:=item.objtype;
		endif
	endforeach
	foreach item in enumerateitemsincontainer(mypack)
		if (item.amount > 1)
			stackeditems[len(stackeditems)+1]:=item.objtype;
		endif
	endforeach

	foreach myitems in enumerateitemsincontainer(mypack)
		if (myitems.container.serial == mypack.serial)
			if ( ( myitems.objtype in stackeditems ) )
				if (CreateItemInContainer( bag, myitems.objtype, myitems.amount ))
					DestroyItem(myitems);
					sleepms(100);
				else
					return;
				endif
			else
				if ( moveitemtocontainer(myitems, bag) )
					sleepms(100);
				else
					return;
				endif
			endif
		endif
	endforeach


endfunction

function grabloot()

	var numitems := 0;
	if (!getobjproperty(me,"serial"))
		setobjproperty(me, "serial", me.serial);
	endif

	var Near_Items := ListItemsNearLocation(me.x, me.y, me.z,6);

     foreach item in Near_Items
	if( CheckLineOfSight( me, item ) )
	
	if ( (item.movable) and ((item.objtype < STARTHAIR or item.objtype > ENDHAIR))  )
		RunToIt(me,item);
		if ( (distance(me, item) < 2) and (moveitemtocontainer(item, mypack)) )
				say( "*yoink*" );
				sleep(1);
		else
			return;
		endif
	elseif (item.objtype == UOBJ_CORPSE)

		// Toadstool -  Don't allow totem to loot corpses inside anti loot areas.
		// 		We let him pick up loose items though, as player can do that
	   if(!IsInAntiLootingArea( item ))
		var items := EnumerateItemsInContainer(item);
		if (len(items))
			RunToIt(me,item);
			if (distance(me, item) < 2)
				foreach thingie in items
					if (thingie.objtype < STARTHAIR or thingie.objtype > ENDHAIR)
					    if (thingie.container.serial == item.serial)
					      if ( moveitemtocontainer(thingie, mypack) )
							say( "*yoink*" );
							sleep(1);
					      endif
					    endif
					endif 
				endforeach
			else
				return;
			endif 
		endif 
	    endif
	endif 

	endif 
     endforeach

endfunction

function RunToIt(me,item)

	var numsteps;
	for (numsteps := 0; numsteps < 6; numsteps := numsteps +1)
		if ( distance(me, item) < 2 )
			return;
		else
			runtoward(item);
		endif
	endfor

endfunction

function teletomaster();

	if (master)
		me.hidden := 0;
		say("Kal Ort Por");
		PlaySoundEffect( me, SFX_210 );
		sleepms(250);
		MoveObjectToLocation( me, master.x, master.y, master.z, master.realm, MOVEOBJECT_FORCELOCATION );
		following := master;
		PlaySoundEffect( me, SFX_SPELL_RECALL );
	endif

endfunction

function Release()

	var it := CreateItemInContainer(master.backpack, 0xff92 ,1);
	if (it)
		say("*Argh*");
		SendSysMessage(master,"Totem released from duty!");
		it.name := me.name;
		PlaySoundEffect( me, SFX_SPELL_DISPEL );
		PlayStationaryEffect( me.x, me.y, me.z, FX_SMOKE, 0xA, 0xA );
		MoveObjectToLocation(me,5288 , 1176 , 0, "britannia", MOVEOBJECT_FORCELOCATION);
		RevokePrivilege( me, "invul" );
		ApplyRawdamage( me , GetMaxHP(me) + 3 );
		EraseObjProperty(master, "humuc");
	else
		say("*Argh*");
		return;
	endif
	
endfunction