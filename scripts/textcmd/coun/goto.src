
use uo;
use os;

include ":staff:include/staff";

program textcmd_goto( character , whom )

	whom := Upper( whom );
	var chr := MakeAndProcessGump( character,whom);
	if( chr )
		LogCommand(character, GetProcess(GetPid()).name, chr.name+"/"+chr.serial, "XYZ - "+chr.x+","+chr.y+","+chr.z);
		MoveObjecttoLocation( character , chr.x , chr.y , chr.z , chr.realm,MOVEOBJECT_FORCELOCATION );
		ShowFlame( character );
	endif

endprogram


function MakeAndProcessGump( who ,Whom)

/* 	var gCmd := GetObjProperty(who, ".goto");
	if(!gCmd)
		SendSysMessage( who , "You havn't been granted this command." );
		LogCommand(who, GetProcess(GetPid()).name, who.name+" is trying to use .goto without permission!");
		return;
	endif
	if(who.cmdlevel < 4 && ReadGameClock() > gCmd)
		SendSysMessage( who , "You can't use this command anymore." );
		EraseObjProperty(who, ".goto");
		return;
	endif */

	var layout	:= array( "page 0",
				  "resizepic 50  0   2600 300 525",
				  "resizepic 100 480 5100 200  25",
				  "text 155 30 5 0");
				
	var data	:= array( "Goto Menu");

	var playername;
	var LastIndex:=0;
	var MaxLen:=len(Whom);
	var players	:= EnumerateOnlineCharacters();

	var start	:= 1;
	var count	:= 0;
	var done	:= 0;
	var page	:= 1;
	var perpage	:= 20;
	var y 	:= 70;
	var i;
	var j;
	var UHunter;
	
	layout.append( "page 1" );
	
	for( i := start ; i <= len(players) ; i := i + 1 )
	if ( players[i].concealed <= who.cmdlevel)
		UHunter:=CInt(GetObjProperty(players[i],"UHunter"));
		playername:=Upper(players[i].name);


		if ((playername[1,MaxLen]==Whom) AND ( (UHunter==0)OR ( who.cmdlevel>3 ) ))
			if( done == perpage )
				done 	:= 0;
				page 	:= page + 1;
				y 	:= 70;
			
				layout.append( "button 250 481 5540 5541 0 " + (page ) ); // Next button for prev page
				layout.append( "page " + page );
				layout.append( "button 125 481 5537 5538 0 " + (page - 1) ); // Prev button for current page
			endif
		
			if (players[i].cmdlevel > 0)
			  layout.append( "text 85 " + y + " 51 " + len(data) );
			else
			  layout.append( "text 85 " + y + " 0 " + len(data) );
			endif
 
			layout.append( "button 300 " + (y+5) + " 2118 2117 1 0 " + i );
			data.append( players[i].name );

			y 	:= y + 20;
			done 	:= done + 1;
			count := count + 1;
			LastIndex:=i;
		endif
	endif
	endfor

	if (count>0)
		var result;
		if (count==1)
			result:=LastIndex;	
		else
			result := SendDialogGump( who , layout , data );
			if( !result[0] )
				return;
			endif
			result := Cint( result[0] );
		endif

		return players[result];
	else
		SendSysMessage(who, "No player with name '"+ whom +"*' was found.");
		return ;
	endif 
endfunction


function ShowFlame( who )

	if( (who.cmdlevel<1) and (who.concealed<>1) )
		var coolflame;
		coolflame := CreateItemAtLocation( who.x, who.y, who.z, 0x3709, 1 );
		PlaySoundEffect(who, 0x208);
		Sleep(3);
		DestroyItem(coolflame);
	endif

endfunction
