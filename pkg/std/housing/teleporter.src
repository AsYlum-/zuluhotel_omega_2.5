use os;
use uo;

include "include/recalling";
include "../pkg/std/housing/utility";

program walk_on_moongate( mobile , gate )
	if( mobile.npctemplate and mobile.script != "tamed" )
		return;
	endif

	if(!mobile.cmdlevel)
		var multi2 := mobile.multi;
		if( multi2 )
			if( !Iscowner( mobile, multi2 )  )
				if( !IsFriend( mobile, multi2, RECALL_TO) )
					SendSysMessage( mobile, "You can't use teleporters in this house." );
					return;
				endif
			endif
		else
			return;
		endif
	endif

	var x := GetObjProperty( gate, "GateDestX" );
	var y := GetObjProperty( gate, "GateDestY" );
	var z := GetObjProperty( gate, "GateDestZ" );

	if( !x or !y )
		return;
	endif

	if( !gate or GetObjProperty( mobile , "JustGated" ) )
		EraseObjProperty( mobile , "JustGated" );
		return;
	endif

	if( IsForbiddenAreaToRecallTo( mobile , x , y )  )
		if (!Getobjproperty(gate, "IgnoreCheck"))
			return;
		endif
	endif

	set_critical(1);
	if( MoveObjectToLocation( mobile , x , y , z ) )
		SetObjProperty( mobile, "JustGated", 1 );
	endif
	
endprogram