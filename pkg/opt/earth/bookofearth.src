use uo;
use os;
use math;
use basic;

include "include/spelldata";

const BASE_SPELLID := 80;

program BookOfEarth( who, book )

	if (GetObjProperty(who, CLASSEID_MAGE))
	elseif (GetObjProperty(who, CLASSEID_POWERPLAYER))
	else
		SendSysMessage(who,"Only Mages and Powerplayers can use this!");
		return;
	endif
	
	if( (book.container).IsA(POLCLASS_CORPSE) )
		SendSysMessage( who, "You can't use this while it is on the corpse." );
		return 0;
	endif

	if( !ReserveItem(book) )
		return 0;
	endif

	var layout := {
		"page 0",
		"nodispose",
		"gumppic 30 30 11055",
		"text 110  40 28 0",
		"text 270 40 28 1"
	};
	
	var data := {
		"Circle 1 Spells",
		"Circle 2 Spells"
	};
	
	var spellnames := {
		"Antidote",		// Circle 1
		"Owl Sight",
		"Shifting Earth",
		"Summon Mammals",
		"Call Lightning",
		"Earth Blessing",
		"Earth Portal",
		"Nature's Touch",
		"Gust of Air",		// Circle 2
		"Rising Fire",
		"Shapeshift",
		"Ice Strike",
		"Earth Spirit",
		"Flame Spirit",
		"Storm Spirit",
		"Water Spirit"
	};
	
	var i;
	var y1 := 70;
	var y2 := 70;
	var bits := { 0x01, 0x02, 0x04, 0x08, 0x10, 0x20, 0x40, 0x80 };
	var greater := Cint(GetObjProperty(book,"Greater"));
	var lesser  := Cint(GetObjProperty(book,"Lesser"));

	for( i := 1; i <= 8; i := i + 1 )
		if( lesser & bits[i] )
			layout.append( "button 90 " + y1 + " 2104 2103 1 0 " + i );
			layout.append( "text 110 " + (y1-5) + " 0 " + len(data) );
			data.append( spellnames[i] );
			y1 := y1 + 20;
		endif
		
		if( greater & bits[i] )
			layout.append( "button 250 " + y2 + " 2104 2103 1 0 " + (i+8) );
			layout.append( "text 270 " + (y2-5) + " 0 " + len(data) );
			data.append( spellnames[i+8] );
			y2 := y2 + 20;
		endif
	endfor

	var spellid := SendDialogGump( who, layout, data )[0];
	if( !spellid )
		return 0;
	endif

	var spell := GetScript( BASE_SPELLID+spellid );
	
	if( GetObjProperty( book , "CastingOpts" ) )
		SetObjProperty( who , "CastingOpts" , GetObjProperty( book, "CastingOpts" ) );
	endif
	
	if(!GetObjProperty(who, "#Casting"))
		SetObjProperty( who , "#Casting" , 1 );
	else
		SendSysMessage(who,"You are already casting something!");
		return;
	endif
	
	Start_Script( spell , who );
		
	while( GetObjProperty( who , "#Casting" ) )
		Sleepms( 500 );
	endwhile

endprogram
