use uo;
use os;


include "include/spelldata";
include "include/classes";
include "include/client";
include "include/dotempmods";
include "include/attributes";

const SPELL_EFFECT_TYPE_MOVING 	:= MOVING_SPELL_WYVERN_STRIKE;
const SPELL_EFFECT_ID		:= FX_SPELL_WYVERN_STRIKE;
const SPELL_EFFECT_SPEED	:= SPEED_SPELL_WYVERN_STRIKE;
const SPELL_EFFECT_LOOP		:= LOOP_SPELL_WYVERN_STRIKE;


program cast_wyvernstrike( parms )

	var circle := GetCircle( SPELLID_WYVERN_STRIKE );
	var plane := NECRO;
	var caster;
	var cast_on;
	var fromhit;

	if (parms[1] == "#MOB")
		caster := parms[2];
        if(IsInSafeArea(caster))
            return;
        endif
		cast_on := parms[3];
		if (parms[4])
			circle := parms[4];
		endif
		if (parms[5])
			fromhit := parms[5];
		endif

	else
		caster := parms;	

		if( TryToCast( caster, SPELLID_WYVERN_STRIKE ) != SUCCESS )
			EraseObjProperty( caster , "#Casting" );
			return;
		endif
		SendSysmessage( caster , "Select a target." );
		cast_on := CanTargetSpell( caster , circle , TGTOPT_CHECK_LOS + TGTOPT_HARMFUL );
		if( !cast_on )
			EraseObjProperty( caster , "#Casting" );
			return;
		endif
	endif
	
	PlaySoundEffect( cast_on , SFX_SPELL_WYVERN_STRIKE );

	if( !fromhit )
		EraseObjProperty( caster , "#Casting" );
		cast_on := Reflected( caster , cast_on , circle );
	endif
		PlayMovingEffect(caster, 
                                 cast_on, 
                                 SPELL_EFFECT_ID, 
                                 SPELL_EFFECT_SPEED, 
                                 SPELL_EFFECT_LOOP, 
                                 explode := 0);
	
	
	PlaySoundEffect( cast_on , SFX_SPELL_WYVERN_STRIKE );

	var protected := IsProtected( caster , cast_on , circle );
	if( protected == IMMUNED )
		EraseObjProperty( caster , "#Casting" );
		return;
	endif
		
	if( protected == CURSED )
		circle := circle * 2;
	endif
	
	var dmg	:= CalcSpellDamage( caster , cast_on , circle );
	var magery	:= GetEffectiveSkill ( caster , SKILLID_MAGERY );
	var plvl	:= ModifyWithMagicEfficiency( caster , Cint( magery /40 ) + 1 );
	var maxx	:= ModifyWithMagicEfficiency( caster , 4 );

	if( protected == CURSED )
		plvl := plvl * 2;
	endif
	
	plvl := Resisted( caster , cast_on , circle , plvl );
	
	if( plvl > maxx )
		plvl := maxx;
	endif
		
	EraseObjProperty( caster , "#Casting" );
	ApplyPlanarDamage( caster , cast_on , circle , dmg , plane );
	var resultprot := GetResultingElementalProt( cast_on, 0x0010 );
	if( plvl && resultprot < 100 )
		SetPoison( cast_on , plvl , "You were poisonned by " + caster.name + "'s Wyvern's Strike spell!", caster );
	endif
	
endprogram
