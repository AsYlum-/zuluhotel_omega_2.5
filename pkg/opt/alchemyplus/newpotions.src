use os;
use uo;
use cfgfile;

include "include/spelldata";
include "include/dotempmods";
include "include/mobileutil";
include "include/drinkpotion";
include "include/classes";
include "include/client";
include "include/attributes";
include "include/res";
include ":gumps:yesno";

const SPELL_EFFECT_ID		:= FX_SPELL_DISPEL;
const SPELL_EFFECT_SPEED	:= SPEED_SPELL_DISPEL;
const SPELL_EFFECT_LOOP		:= LOOP_SPELL_DISPEL;

program use_product (me, what)

	if( !ReserveItem(what) )
		return;
	endif

	if( !CheckIfCanDrink(me) )
		return;
	endif

	// Toadstool - Below code doesn't work, no idea who did it
	if (cint(GetObjProperty(what,"itemtype"))==33)
		SetObjProperty( me , "#potion" , ReadGameClock() + (DELAY_BETWEEN_DRINK*2) );
	endif

	if( IsPoison( what ) )
		DoPoison( me , what );
	else
		var item := FindItemType(what);

		if( !item )
			PrintTextAbovePrivate(me,"Nothing happens",me);
		else
			PlayEffects( me , item );

			if( item.usecreate )
				CreateItemInContainer(me.backpack,item.usecreate,1);
			endif

			if( NukeTotemIngredient(me,item) )
				DoEffect( me , item , what );
			endif
		endif
	endif

endprogram

function FindItemType(what)

	var itemtype := cint(GetObjProperty(what,"itemtype"));
	var items := ReadConfigFile( "alchemyplus" );
	return FindConfigElem(items,itemtype);

endfunction

function PlayEffects(me,item)

	if (item.usenoise)
		PlaySoundEffect(me,item.usenoise);
	endif

	if (item.useanim)
		PerformAction(me,item.useanim);
	endif

	if (item.useeffect)
		PlayObjectCenteredEffect( me, item.useeffect, 10,10);
	endif

endfunction

function DoEffect( me , item , what )

	if (item.skillcheck)
		if( !CheckSkill( me , SKILLID_ALCHEMY , item.skillcheck , item.skillcheck * 5 ) )
			if( GetObjProperty( me , CLASSEID_MAGE ) )
				if( !CheckSkill( me , SKILLID_ALCHEMY , item.skillcheck , item.skillcheck * 5 ) )
					DoBadThingTo(me,Random(cint(item.skillcheck/5)));
					SubtractAmount(what, 1);
					return;
				endif
			else
				DoBadThingTo(me,Random(cint(item.skillcheck/5)));
				SubtractAmount(what, 1);
				return;
			endif
		endif
	endif

	case (item.effect)
		"invis":	DoInvisEffect(me , what );		break;
		"cunning":	DoCunningEffect( me , item , what );	break;
		"protection":	DoProtectionEffect( me , item , what );	break;
		"bless":	DoBlessEffect( me , item , what );	break;
		"fullheal":	DoFullHeal( me , what );		break;
		"fullmana":	DoFullMana( me , what );		break;
		"dispel":	DoDispel( me , what );		break;
		"resurrect":	DoRess( me , what );			break;
		"poly":		DoPolyEffect( me , item , what );	break;
		"totem":	MakeTotem( me , what );			break;
		default:	PrintTextAbovePrivate(me,"Nothing happens",me);
		SubtractAmount(what, 1);
	endcase

endfunction

// Note: Invis pots DON't get the truemage prop. Do I didn't alter this for it
// May need to later though!
function DoInvisEffect( me , what )

	detach();
	var duration := 30;
	if( GetObjProperty( what , "ByTrueMage" ) )
		SendSysMessage( me , "This potion is so potent that it contains 2 doses." );
		EraseObjProperty( what , "ByTrueMage" );
		ReleaseItem( what );
		duration := 60;
	else
		SubtractAmount(what, 1);
	endif

	SetObjProperty(me, "StealthCD", ReadGameClock()+10);
	me.hidden := 1;

	Sleep( duration );
	
	me.hidden := 0;

endfunction

function DoCunningEffect( me , item , what )

	var strength	 := GetPotionStr( item , what );
	var mod_amount := RandomDiceStr(strength+"d10");
	var duration   := strength * 20;
	if( CanMod( me , "int" ) )
		DoTempMod( me , "int" , mod_amount , duration );
	endif
	SubtractAmount(what, 1);

endfunction

// AGAIN, dispel pots don't have TrueMage prop. Don't need to alter it YET
function DoDispel( me , what)

	PlayObjectCenteredEffect(me, 
                               SPELL_EFFECT_ID,
                               SPELL_EFFECT_SPEED,
                               SPELL_EFFECT_LOOP);
	PlaySoundEffect( me, SFX_SPELL_DISPEL );
	WipeMods( me );
	if( me.graphic != me.trueobjtype and !me.dead )
		me.graphic := me.trueobjtype;
	endif
	SendSysmessage( me , "All the magical effects active on this creature have been wiped." );

	// THIS IS NEVER HIT
	if( GetObjProperty( what , "ByTrueMage" ) )
		EraseObjProperty( what , "ByTrueMage" );
		SendSysMessage( me , "This potion is so potent, that only half of it is needed to remove all magic property's on you." ); 
		ReleaseItem( what );
	else
		SubtractAmount(what, 1);
	endif

endfunction


function DoProtectionEffect( me , item , what )

	var strength	 := GetPotionStr( item , what );
	var mod_amount := RandomDiceStr(strength+"d2");
	var duration   := strength * 15;
	if( CanMod( me , "ar" ) )
		DoTempMod(me, "ar", mod_amount, duration);
	endif
	SubtractAmount(what, 1);

endfunction

function DoBlessEffect( me , item , what )

	var strength	 := GetPotionStr( item , what );
	var mod_amount := RandomDiceStr(strength+"d3");
	var duration   := strength * 15;
	if( CanMod( me , "all" ) )
		DoTempMod(me, "all", mod_amount, duration);
	endif
	SubtractAmount(what, 1);

endfunction

function DoFullHeal( me , what )

	SetHP(me, GetMaxHP(me));
	if( GetObjProperty( what , "ByTrueMage" ) )
		SendSysMessage( me , "This potion is so potent, that only half of it is needed to refresh your health." ); 
		ReleaseItem( what );

		CreateItemInBackpack(me, 0xff86, 1); // Create a normal tamla potion, so we don't break the stack
	endif
	
	SubtractAmount(what, 1);

endfunction

function DoFullMana( me , what )

	SetMana(me, GetMaxMana(me)); 
	if( GetObjProperty( what , "ByTrueMage" ) )
		SendSysMessage( me , "This potion is so potent, that only half of it is needed to refresh your mana." ); 
		ReleaseItem( what );

		CreateItemInBackpack(me, 0xff74, 1); // Create a normal GMR potion, so we don't break the stack

	endif
		
	SubtractAmount(what, 1);
	
endfunction

function DoBadThingTo(me, HowBad)

	if (HowBad < 6)
		PrintTextAbovePrivate(me,"Gah!  Your hair falls out!",me);
		PrintTextAbovePrivate(me,"Gah!  That did not work!",me);
		DestroyItem(GetEquipmentByLayer(me,LAYER_HAIR));
		DestroyItem(GetEquipmentByLayer(me,LAYER_BEARD));
		
	elseif (HowBad < 12)
		PrintTextAbovePrivate(me,"Ugh... Your don't feel so well...",me);
		if( CanMod( me , "all" ) )
			DoTempMod(me,"all", 0-(Random(20)+1),200+Random(400) );
		endif
		
	elseif (HowBad < 10)
		PrintTextAbovePrivate(me,"Doh!",me);
		DoPersistedMod(me,"color",Random(47)+1301,me.color,200+Random(400) );

	else
		var x := cint(HowBad/4);
		SetPoison(me, x, "Gack! You've been poisoned!");
	endif

endfunction

function DoPolyEffect( me , item , what )

	var strength	 := GetPotionStr( item , what );
	var critter 	 := Random(8);
	var newcolor 	 := 1401+(strength*10)+critter;
	var mod_amount := (strength*5)+critter;
	var duration 	 := strength * 120;
	if( CanMod( me , "poly" ) )
		DoTempMod( me , "poly" , mod_amount , duration );
		if( CanMod( me , "color" , 1 ) )
			DoPersistedMod( me , "color" , newcolor , me.color,duration );
		endif
	endif
	SubtractAmount(what, 1);

endfunction

function GetPotionStr( item , what )

	var strength := item.strength;
	var bonus := Cint(GetObjProperty( what , "ByTrueMage" ));
	if( strength )
		return (strength + bonus);
	else
		return 0;
	endif

endfunction

function MakeTotem( me , what )

	if(!GetObjProperty( me , CLASSEID_MAGE ) || GetClasseLevel(me) < 4)
		SendSysMessage(me, "You need to be at least level 4 mage to do that.");
		return 0;
	endif
	SubtractAmount(what, 1);
	
	set_critical(1);
	detach();
	var parms := struct;
	parms.+ script := "humuc";

	var thecritter := CreateNPCfromTemplate("humuc", cint(me.x - 1), me.y, me.z, parms);
	
	if (!thecritter)
		thecritter := CreateNPCfromTemplate("humuc", me.x, cint(me.y - 1), me.z, parms);
		if (!thecritter)
			return;
		endif
	endif

	SetObjProperty( thecritter , "master" , me.serial );
	SetObjProperty( thecritter , "script" , thecritter.script );
	SetObjProperty( me, "humuc", cint(thecritter.serial) );
   
	thecritter.setmaster( me );

	thecritter.script := "humuc";
	thecritter.name := me.name;

	RestartScript(thecritter);
	
	CopySkills( me , thecritter );
	if( GetObjProperty( me , CLASSEID_MAGE ) )
		SetObjProperty( thecritter , CLASSEID_MAGE , GetObjProperty( me , CLASSEID_MAGE ) );
		SetStrengthMod(thecritter, GetStrengthMod(thecritter)+ GetStrength(me) );

		SetObjProperty( thecritter , "BaseStrmod" , GetObjProperty( thecritter , "BaseStrmod" ) + GetStrength(me));
		SetIntelligenceMod(thecritter, GetIntelligenceMod(thecritter)+GetIntelligence(me) );
		
		SetObjProperty( thecritter , "BaseIntmod" , GetObjProperty( thecritter , "BaseIntmod" ) + GetIntelligence(me));
		
		SetDexterityMod(thecritter, GetDexterityMod(thecritter)+GetDexterity(me) );
		SetObjProperty( thecritter , "BaseDexmod" , GetObjProperty( thecritter , "BaseDexmod" ) + GetDexterity(me));
	endif
	
	set_critical(0);

endfunction


function NukeTotemIngredient(me,item)

	if(item.itemtype == 36 || item.itemtype == 37)
		if(!GetObjProperty( me , CLASSEID_MAGE ) || GetClasseLevel(me) < 4)
			SendSysMessage(me, "You need to be at least level 4 mage to do that.");
			return 0;
		endif
	endif
	var findtype;
	if (item.itemtype == 36)
		findtype := 37;
	elseif (item.itemtype == 37)
		findtype := 36;
	else
		return 1;
	endif

	if (GetObjProperty(me, "humuc") )
		if ( SystemFindObjectBySerial(cint(GetObjProperty(me, "humuc"))) )
			SendSysMessage(me,"You already have a pet!");
			return 0;
		endif
	endif

	var otherthing;
	var thingfound := 0;
	foreach otheritem in EnumerateitemsinContainer(me.backpack)
		if (otheritem.objtype == 0xff93 || otheritem.objtype == 0xff92)
			otherthing := FindItemType(otheritem);
			if (otherthing.itemtype == findtype)
				thingfound := 1;
				SubtractAmount(otheritem, 1);
				return 1;
			endif
		endif
	endforeach
	
	if (!thingfound)
		SendSysMessage(me,"You don't have all the ingredients!");
		return 0;
	else
		return 0;
	endif

endfunction

function DoRess( me , what)

	SendSysmessage( me, "Target a pet(corpse), player(ghost), Warrior for Hire(heart) or Mount Stone.", FONT_NORMAL, 2601 );
	var patient := Target( me, TGTOPT_NEUTRAL + TGTOPT_CHECK_LOS);

	if( me.dead )
		SendSysMessage( me , "I'm dead and cannot do that.", FONT_NORMAL, 2595 );
		return;
	endif

	if( Distance( me , patient ) > 2 )
		SendSysMessage( me , "The target is too far.", FONT_NORMAL, 2595 );
		return;
	endif

	// Remove a resurrection counter from Warrior For Hire.
	if(patient.objtype == 0x1CED)
		if( YesNo( me, "Pay 10.000 gold?") )
			if(GetObjProperty( patient, "resnum" ))
				if(me.spendgold(10000))
					var resnum := GetObjProperty( patient, "resnum" );
					SetObjProperty(patient, "resnum", resnum -= 1);
					SendSysMessage( me , "You removed a resurrection counter from "+patient.name+"!", FONT_NORMAL, 2601 );
					SendSysMessage( me , "It currently has " +resnum+ " resurrection counters.", FONT_NORMAL, 2567 );
					SubtractAmount(what, 1);
				else
					SendSysMessage(me, "You do not have enough money!", FONT_NORMAL, 2595 );
				endif
				return;
			else
				SendSysMessage( me , patient.name+" has no resurrection counters! (No gold was spend)", FONT_NORMAL, 2595 );
				return;
			endif
		else
			SendSysMessage( me, "Canceled", FONT_NORMAL, 2595 );
			return;
		endif
	endif

	//Add a rebirth charge to a mount stone
	if(GetObjProperty( patient, "MountTile" ))
		var curr_charges := GetObjProperty( patient, "charges" );
		if(!curr_charges)
			curr_charges := 0;
		endif
		if(curr_charges >=10)
			SendSysMessage( me ,"The "+what.name+" is full of Rebirth Potions already.", FONT_NORMAL, 100);
		else
			SetObjProperty( patient, "charges",  curr_charges+1);
			SubtractAmount(what, 1);
			SendSysMessage( me, "The "+patient.name+" now has "+(curr_charges+1)+" Rebirth Potions.", FONT_NORMAL, 100);
			EraseObjProperty(me, "#potion"); //no reason to delay a recharge
		endif
		return;
	endif

	var delay 	:= 10;
	var shp		:= GetHP(me);
	var x 		:= me.x;
	var y 		:= me.y;

	// NPC-Pet is owned by user.
	if( patient.objtype == UOBJ_CORPSE )
		if(GetObjProperty( patient, "master" ) == me.serial)
			var npcscript := GetObjProperty(patient, "npctemplate");
			if(npcscript == "humuc" || npcscript == "warriorforhire")
				SendSysMessage(me, "You can't resurrect this.", FONT_NORMAL, 2595 );
				return;
			else
				if (GetObjProperty(patient, "#Ressing"));
					SendSysMessage(me, "This creature is currently being aided back to life.", FONT_NORMAL, 2595 );
					return;			
				else
					SetObjProperty(patient, "#Ressing", 1);
					SendSysMessage( me , "Attempting to revive " +patient.name+ "!", FONT_NORMAL, 2601 );
					while(delay)
						Sleep(1);
						delay := delay - 1;
						if(delay == 5)
							SendSysMessage( me , "Almost there...", FONT_NORMAL, 2567 );
						endif
						if( GetHP(me) < shp )
							SendSysMessage( me , "Your were interrupted, resurrection stopped.", FONT_NORMAL, 2595 );
							EraseObjProperty(patient, "#Ressing");
							return;
						elseif(me.x != x || me.y != y )
							SendSysMessage( me , "You moved, resurrection stopped.", FONT_NORMAL, 2595 );
							EraseObjProperty(patient, "#Ressing");
							return;
						endif
					endwhile

					var npc := CreateNPCFromTemplate(GetObjProperty(patient, "npctemplate"), patient.x, patient.y, patient.z);
					if(npc)
						var color := getobjproperty(patient,"color");
						DestroyItem(patient);
						SetObjProperty(npc, "nocut", 1);
						SetObjProperty(npc, "noloot", 1);
						SetObjProperty(npc, "VetResed", 1);
						SetObjProperty( npc , "master" , me.serial );
						SetObjProperty( npc , "script" , npc.script );
						npc.setmaster( me );
						npc.script := "tamed";
						RestartScript(npc);
						if(color != npc.color)
							npc.color := color;
							setobjproperty(npc,"color", color);
						endif
						SendSysMessage( me , "You resurrected "+npc.name+"!", FONT_NORMAL, 2601 );
					endif
				endif
			endif
		else
			SendSysMessage( me , "You can't resurrect this!", FONT_NORMAL, 2595 );
			return;
		endif
	// Player
	elseif( patient.dead )
		if(!patient.isa(POLCLASS_NPC))
			if (GetObjProperty(patient, "#Ressing"));
				SendSysMessage(me, patient.name+" is currently being aided back to life.", FONT_NORMAL, 2595 );
				return;			
			else
				SetObjProperty(patient, "#Ressing", 1);
				SendSysMessage( me , "Attempting to resurrect!", FONT_NORMAL, 2601 );
				while(delay)
					Sleep(1);
					delay := delay - 1;
					if(delay == 5)
						SendSysMessage( me , "Almost there...", FONT_NORMAL, 2567 );
					endif
					if( GetHP(me) < shp )
						SendSysMessage( me , "Your were interrupted, resurrection stopped.", FONT_NORMAL, 2595 );
						EraseObjProperty(patient, "#Ressing");
						return;
					elseif(me.x != x || me.y != y )
						SendSysMessage( me , "You moved, resurrection stopped.", FONT_NORMAL, 2595 );
						EraseObjProperty(patient, "#Ressing");
						return;
					endif
				endwhile

				ResurrectMobile( patient , me );
				SendSysMessage( me , "You resurrected "+patient.name+"!", FONT_NORMAL, 2601 );
				SendSysMessage( patient , me.name+" resurrected you with a rebirth potion!", FONT_NORMAL, 2601 );
			endif
		else
			SendSysMessage( me , "You cannot revive this!", FONT_NORMAL, 2595 );
			return;
		endif
	else
		SendSysMessage( me , "That isn't dead!", FONT_NORMAL, 2595 );
		return;
	endif

	PlayObjectCenteredEffectEx( patient, SPELL_EFFECT_ID, SPELL_EFFECT_SPEED, SPELL_EFFECT_LOOP );
	PlaySoundEffect(me, SFX_SPELL_RESURRECTION);
	PlaySoundEffect(patient, SFX_SPELL_RESURRECTION);
	EraseObjProperty(patient, "#Ressing");

	SubtractAmount(what, 1);

endfunction