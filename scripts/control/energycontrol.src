use uo;
use os;
use cfgfile;

include "include/spelldata";
include "include/objtype";
include "include/classes";
include "include/client";
include "include/random";
const DELAY_BETWEEN_HITS_MS	:= 1000;

program energy_control( item )

	//return 1;
	var diff    := 60;
	var points  := 25;
	var power   := 4;
	var element := AIR;
	var creator := 0;
	var serial  := Cint( GetObjProperty( item , "Creator" ) );
	
	if( GetObjProperty( item , "ByTrueMage" ) )
		power := Cint( power * CLASSE_BONUS );
	endif
		
	if( serial )
		creator := SystemFindObjectBySerial( serial );
		if( !creator ) 
			creator := SystemFindObjectBySerial( serial , SYSFIND_SEARCH_OFFLINE_MOBILES );
		endif
	
		if(creator)
			SetScriptController(creator);
		endif
	endif

	var coordinates := {};
	
	case( item.objtype )
		UOBJ_EFIELD_NS:	coordinates := { { item.x , item.y , item.z },
						 { item.x , item.y-1 , item.z },
						 { item.x , item.y+1 , item.z } };
				break;
				 
				 
		UOBJ_EFIELD_EW:	coordinates := { { item.x , item.y , item.z },
						 { item.x-1 , item.y , item.z },
						 { item.x+1 , item.y , item.z } };
				break;
				
		default:
	endcase

	if( !len(coordinates) )
		return;
	endif
		
	while( item )
		var dmg := RandomDiceStr( power + "d8" );
		PlaySoundEffect( item , SFX_SPELL_ENERGY_FIELD );
		foreach coord in coordinates
			var victims := ListMobilesNearLocation( coord[1] , coord[2] , coord[3] , 0 );
			victims := SmartAoE(creator, victims);
			foreach mobile in victims
				if( !creator )
					creator := mobile;
				endif
				if(!mobile.multi)
					PerformAction( mobile , ANIM_HIT );
					PlayObjectCenteredEffectEx( mobile, 0x3789, 0 , 10, 496, 5);
					ApplyElementalDamageNoResist( mobile,creator,Cint(ResistedDefinedSkillGain( creator , mobile , dmg , diff , points )),element );
				endif
			endforeach
		endforeach
		Sleepms( DELAY_BETWEEN_HITS_MS );
	endwhile
  
endprogram