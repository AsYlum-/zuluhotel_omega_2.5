use uo;

include "include/spelldata";
include "include/client";
include "include/objtype";
include "include/skillpoints";
include "include/bard";
include "include/eventid";
include "include/checkcity";
include "include/dotempmods";

const SPELL_EFFECT_TYPE_MOVING 	:= MOVING_SPELL_PARALYZE;
const SPELL_EFFECT_ID		:= FX_SPELL_PARALYZE;
const SPELL_EFFECT_SPEED	:= SPEED_SPELL_PARALYZE;
const SPELL_EFFECT_LOOP		:= LOOP_SPELL_PARALYZE;

program cast_songoffright( player )
	// Disabled until frightai is fixed Error is below:
	// Script Error in 'pkg/opt/songbook/frightai.ecl' PC=34: Call to function SystemFindObjectBySerial
	/*
	SendSysMessage(player, "Due to a bug this song is temporarily disabled!", FONT_NORMAL, 2595 );
	EraseObjProperty( player , "#Casting" );	
	return;
	*/

	var singinghp := GetHp( player );
	var configfile := ReadConfigFile("::npcdesc");
	var circle := GetCircle( SPELLID_SONG_OF_FRIGHT  );
	var instrument := findinstrument( player );
	var sfx_success;
	var sfx_failure;
	
	SetObjProperty( player , "#Casting", 1 );
	case( instrument.graphic )
		UOBJ_HARP:	sfx_success := SFX_HARP_SUCC;
				sfx_failure := SFX_HARP_FAIL;
				break;
				
		UOBJ_LUTE:	sfx_success := SFX_LUTE_SUCC;
				sfx_failure := SFX_LUTE_FAIL;
				break;
				
		UOBJ_DRUM:	sfx_success := SFX_DRUM_SUCC;
				sfx_failure := SFX_DRUM_FAIL;
				break;
				
		UOBJ_TAMB:	sfx_success := SFX_TAMB_SUCC;
				sfx_failure := SFX_TAMB_FAIL;
				break;

		default:	PrintTextAbovePrivate( player, "You dont have an Instrument!",player );
					EraseObjProperty( player , "#Casting" );
		return;
	endcase
	
	var peace := GetAttribute(player, ATTRIBUTEID_PEACEMAKING);
	var mus := GetAttribute(player, ATTRIBUTEID_MUSICIANSHIP);
	var spec := GetObjProperty(player,"IsBard");
	var ps;
	var drain := 10 + (circle*2)-(spec+5);
	var difficulty := (circle*12)+(Random(25)-(spec*8));
		
	if (!spec)
		spec := 0;	
	endif

	//PrintTextAbove(player,"Difficulty:"+difficulty);
	PerformAction( player, 0X021 );
	if (difficulty > peace or difficulty > mus or !spec)
		PlaySoundEffect( player , sfx_failure );
		PrintTextAbove(player,"*"+player.name+" plays a song poorly*");
		ps := GetStamina( player ) -(drain/2);
		if (ps < 0 )
			ps := 0;
		endif
		SetStamina( player, ps );
		EraseObjProperty( player , "#Casting" );
		return;
	elseif ((GetStamina( player ) -drain) <  0)
		PrintTextAbove(player,"*"+player.name+" plays a song poorly*");
		PrintTextAbovePrivate( player, "Out of Stamina!",player );
		PlaySoundEffect( player , sfx_failure );
		EraseObjProperty( player , "#Casting" );
		return;
	else		
		ps := GetStamina( player ) -drain;
		if (ps < 0 )
			ps := 0;
		endif
		SetStamina( player, ps );
		sleep(1);
		if (GetHp( player ) < singinghp)
			PrintTextAbove(player,"*"+player.name+" plays a song poorly*");
			PrintTextAbovePrivate( player, "You were interrupted!",player );
			PlaySoundEffect( player , sfx_failure );
			EraseObjProperty( player , "#Casting" );
			return;
		else
			PerformAction( player, 0X021 );
			PlaySoundEffect( player , sfx_success );
			PrintTextAbove(player,"Refugio Iniquus Abutor Reverentia");
			sleep(2);
			var duration := (peace/250)+(spec*4);
			EraseObjProperty( player , "#Casting" );
			
			var affected := ListMobilesInLineOfSight( player , cint( (spec+1) * 2 ));
			foreach cast_near in affected
				if(!ValidSongTarget(player, cast_near))
					continue;
				endif
				if (!cast_near.isa(POLCLASS_NPC))
					continue;
				endif
				var elem := FindConfigElem( configfile, cast_near.npctemplate );
				if((cast_near.script != ":songbook:frightai") && (elem.Privs != "invul"))
					if(!GetObjProperty(cast_near, "Boss") && !GetObjProperty(cast_near, "SuperBoss"))
						difficulty := Cint( elem.provoke );
						if( CheckSkill( player, SKILLID_PEACEMAKING, difficulty, 0 ) )
							PrintTextAbove(cast_near, "*"+cast_near.name+" flees in fright!*"); 
							SetObjProperty(cast_near, "originalscript",cast_near.script);
							SetObjProperty(cast_near, "frightlength", duration);
							SetObjProperty(cast_near, "runfrom", player.serial);
							cast_near.script := ":songbook:frightai";
							RestartScript(cast_near);
						endif
					endif
				endif
			endforeach
		endif
	endif
	EraseObjProperty( player , "#Casting" );
endprogram
