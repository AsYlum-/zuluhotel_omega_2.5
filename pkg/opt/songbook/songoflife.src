use uo;

include "include/spelldata";
include "include/client";
include "include/objtype";
include "include/skillpoints";
include "include/bard";
include "include/eventid";
include "include/checkcity";
include "include/dotempmods";
include "include/damages";

const SPELL_EFFECT_TYPE_MOVING 	:= MOVING_SPELL_TOUCH_OF_GOD;
const SPELL_EFFECT_ID		:= FX_SPELL_TOUCH_OF_GOD;
const SPELL_EFFECT_SPEED	:= SPEED_SPELL_TOUCH_OF_GOD;
const SPELL_EFFECT_LOOP		:= LOOP_SPELL_TOUCH_OF_GOD;

program cast_songoflife( player )
	var singinghp := GetHp( player );
	SetObjProperty( player , "#Casting", 1 );
	var circle := GetCircle( SPELLID_SONG_OF_LIFE  );
	var instrument := findinstrument( player );
	var sfx_success;
	var sfx_failure;
	case( instrument.graphic )
		UOBJ_HARP:	sfx_success := SFX_HARP_SUCC;
				sfx_failure := SFX_HARP_FAIL;
				break;
				
		UOBJ_LUTE:	sfx_success := SFX_LUTE_SUCC;
				sfx_failure := SFX_LUTE_FAIL;
				break;
				
		UOBJ_DRUM:	sfx_success := SFX_DRUM_SUCC;
				sfx_failure := SFX_DRUM_FAIL;
				break;
				
		UOBJ_TAMB:	sfx_success := SFX_TAMB_SUCC;
				sfx_failure := SFX_TAMB_FAIL;
				break;
				
		default:	PrintTextAbovePrivate( player, "You dont have an Instrument!",player );
				EraseObjProperty( player , "#Casting" );
				return;
	endcase
	

	var peace := GetAttribute(player, ATTRIBUTEID_PEACEMAKING);
	var mus := GetAttribute(player, ATTRIBUTEID_MUSICIANSHIP);
	var spec := GetObjProperty(player,"IsBard");
	var ps;
	if (!spec)
		spec := 0;	
	endif
	var dmod;
	var duration;
	var proped := array();
	var drain :=  10 + (circle*2)-(spec+5);
	var difficulty := (circle*9)+(Random(25)-(spec*8));
	var healed;
	var bonus;
	//PrintTextAbove(player,"Difficulty:"+difficulty);
	PerformAction( player, 0X021 );
	if (difficulty > peace or difficulty > mus or !spec)
		PlaySoundEffect( player , sfx_failure );
		EraseObjProperty( player , "#Casting" );
		PrintTextAbove(player,"*"+player.name+" plays a song poorly*");
		ps := GetStamina( player ) -(drain/2);
		if (ps < 0 )
			ps := 0;
		endif
		SetStamina( player, ps );
		return;
	elseif ((GetStamina( player ) -drain) <  0)
		PrintTextAbove(player,"*"+player.name+" plays a song poorly*");
		PrintTextAbovePrivate( player, "Out of Stamina!",player );
		PlaySoundEffect( player , sfx_failure );
	else
		ps := GetStamina( player ) -drain;
		if (ps < 0 )
			ps := 0;
		endif
		SetStamina( player, ps );
			sleep(1);
//		if (GetHp( player ) < singinghp)
//			PrintTextAbove(player,"*"+player.name+" plays a song poorly*");
//			PrintTextAbovePrivate( player, "You were interrupted!",player );
//			PlaySoundEffect( player , sfx_failure );
//			EraseObjProperty( player , "#Casting" );
//			return;
//		else
			PerformAction( player, 0X021 );	
			PlaySoundEffect( player , sfx_success );
			PrintTextAbove(player,"Votum Spiritus Medicor Plaga");
//		endif
		sleep(2);
		dmod := (peace/6) + spec*10;
		duration := dmod*30;
//		needs to be 0 for the message  -  ClasH
		duration := 0;
		var affected := ListMobilesInLineOfSight( player , cint( (spec+1) * 2 )) + player;

		affected := SmartBoost(player, affected);

		foreach cast_near in affected
			if(!ValidSongBoost(player, cast_near))
				continue;
			endif

			PlayObjectCenteredEffect(cast_near, 
			SPELL_EFFECT_ID,
			SPELL_EFFECT_SPEED,
			SPELL_EFFECT_LOOP);
			PlaySoundEffect( cast_near , SFX_SPELL_TOUCH_OF_GOD );
			healed := (peace/6) + (spec*10);
			bonus := GetObjProperty( cast_near , "HealingIncrease" );
			if( bonus )
				healed := healed + Cint( healed * bonus * 0.1 );
			endif
			if( healed < 0 )
				healed := 0;
			endif
			HealDamage( cast_near , healed );
		endforeach
	endif
	EraseObjProperty( player , "#Casting" );
		
endprogram
