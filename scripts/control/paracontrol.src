use uo;
use os;
use cfgfile;

include "include/dotempmods";
include "include/classes";
include "include/client";
include "include/spelldata";

const DELAY_BETWEEN_CHECKS_MS	:= 1000;

program para_control( item )

	var diff     := 50;
	var points   := 25;
	var duration := 10;
	var creator := 0;
	var serial  := Cint( GetObjProperty( item , "Creator" ) );
	
	if( GetObjProperty( item , "ByTrueMage" ) )
		diff := Cint( diff * CLASSE_BONUS );
		points := Cint( points / CLASSE_BONUS );
		duration := Cint( duration * CLASSE_BONUS );
	endif
	
	if( serial )
		creator := SystemFindObjectBySerial( serial );
		if( !creator ) 
			creator := SystemFindObjectBySerial( serial , SYSFIND_SEARCH_OFFLINE_MOBILES );
		endif
	
		if(creator)
			SetScriptController(creator);
		endif
	endif
	
	while( item )
		PlaySoundEffect( item , SFX_SPELL_PARALYZE_FIELD );
		var victims := ListMobilesNearLocation( cint(item.x) , cint(item.y) , cint(item.z) , 0 );
		victims := SmartAoE(creator, victims);
		foreach mobile in victims
			if( CanMod( mobile , "p" ) )
				if( !GetObjProperty( mobile , "FreeAction" ) )
					if( !CheckSkill( mobile , SKILLID_MAGICRESISTANCE , diff , points ) )
						PlayObjectCenteredEffect( mobile , FX_SPELL_PARALYZE , SPEED_SPELL_PARALYZE , LOOP_SPELL_PARALYZE );
						PlaySoundEffect( mobile , SFX_SPELL_PARALYZE );
						if(!mobile.multi)
							DoTempMod( mobile , "p" , 1 , duration );
						endif
					endif
				endif
			endif
		endforeach
		
		Sleepms( DELAY_BETWEEN_CHECKS_MS );
	endwhile
  
endprogram