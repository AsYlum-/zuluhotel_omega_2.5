use uo;
use os;
use vitals;

program angry_bees (bees)

	//the amount of time between bee attacks (in seconds), how much damage they do, and how long before they settle down
	var time_between_attacks := 15;
	var attack_damage := 1;
	var attack_length := 60;

	//setup for how long the bees last
	var start_time := ReadGameClock();
	var current_time := ReadGameClock();

	var creator_serial := getobjproperty (bees, "creator");
	var creator := SystemFindObjectBySerial(cint(creator_serial));
	if (creator)
		SetScriptController(creator);
	endif

	repeat
		foreach mobile in ListMobilesNearLocation( bees.x, bees.y, bees.z, 2 )
			ApplyRawDamage (mobile, attack_damage);
		endforeach
		sleep (time_between_attacks);
		current_time := ReadGameClock();

		PrintTextAbove (bees, "*BUZZ*");
	until ( current_time > start_time + attack_length );
	DestroyItem (bees);
endprogram